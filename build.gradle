plugins {
    id 'com.github.johnrengelman.shadow' version '8.0.0'
    id 'java'
}

group 'com.froobworld'
version '1.5.4'
jar.enabled = false

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots" }
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://papermc.io/repo/repository/maven-public/" }
    maven { url "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.3-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.2'
    compileOnly 'io.netty:netty-all:4.1.89.Final'
    implementation 'org.jooq:joor-java-8:0.9.14'
    implementation 'com.froobworld:nabconfiguration:1.0.2'
    implementation 'org.bstats:bstats-bukkit:3.0.1'
}

processResources {
    filter { String line -> line.replace('${version}', project.version.toString()) }
}

shadowJar {
    archiveFileName = 'ViewDistanceTweaks-' + project.version.toString() + '.jar'

    relocate 'com.froobworld.nabconfiguration', 'com.froobworld.viewdistancetweaks.lib.nabconfiguration'
    relocate 'org.joor', 'com.froobworld.viewdistancetweaks.lib.joor'
    relocate 'org.bstats', 'com.froobworld.viewdistancetweaks.lib.bstats'
    relocate 'org.bstats.bukkit', 'com.froobworld.lib.bstats'
}

artifacts {
    shadowJar
}

assemble.dependsOn(shadowJar)